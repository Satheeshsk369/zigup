const std = @import("std");
const adt = @import("adt");

pub const Set = adt.Set(null);

pub fn Group(comptime E: type, comptime Payload: type, comptime label: ?[]const u8) type {
    return struct {
        pub const Type = E;
        pub const Union = Set.enumToUnion(E, Payload);
        pub const argLabel = label;
    };
}

pub const A = enum(u2) {
    help,
    version,
    env,
    update,

    pub fn info(self: @This()) []const u8 {
        return switch (self) {
            .help => "Print this message",
            .version => "Print zigup tool version",
            .env => "Print configuration and environment paths",
            .update => "Update zigup to the latest release version",
        };
    }
};

pub const C = enum(u2) {
    install,
    delete,
    default,
    list,

    pub fn info(self: @This()) []const u8 {
        return switch (self) {
            .install => "Download and install a version",
            .delete => "Delete an installed version",
            .default => "Set an installed version as the active default",
            .list => "List local installs (or remote versions if mirror is specified)",
        };
    }
};

pub const GroupA = Group(A, void, null);
pub const GroupC = Group(C, []const u8, "<TAG>");

pub const Command = Set.join(GroupA.Union, GroupC.Union);

pub const Entry = struct { verb: []const u8, argLabel: ?[]const u8, description: []const u8 };

fn appendEntries(comptime G: type, comptime out: []Entry, comptime start: usize) usize {
    var i = start;
    for (@typeInfo(G.Type).@"enum".fields) |f| {
        const v: G.Type = @enumFromInt(f.value);
        const label = if (std.mem.eql(u8, f.name, "list")) "<MIRROR>" else G.argLabel;
        out[i] = .{ .verb = f.name, .argLabel = label, .description = v.info() };
        i += 1;
    }
    return i;
}

pub const commands: []const Entry = blk: {
    const n = @typeInfo(A).@"enum".fields.len +
        @typeInfo(C).@"enum".fields.len;
    var out: [n]Entry = undefined;
    var i: usize = 0;
    i = appendEntries(GroupA, &out, i);
    i = appendEntries(GroupC, &out, i);
    const frozen = out;
    break :blk &frozen;
};
